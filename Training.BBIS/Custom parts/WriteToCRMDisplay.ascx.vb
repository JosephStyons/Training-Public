Imports BBNCExtensions
Imports Blackbaud.AppFx.WebAPI


Partial Public Class WriteToCRMDisplay
    Inherits BBNCExtensions.Parts.CustomPartDisplayBase

    Private mContent As WriteToCRMProperties

    Protected Sub Page_Load(ByVal sender As Object, ByVal e As System.EventArgs) Handles Me.Load
    End Sub

    Private ReadOnly Property MyContent() As WriteToCRMProperties
        Get
            If mContent Is Nothing Then
                mContent = Me.Content.GetContent(GetType(WriteToCRMProperties))
                If mContent Is Nothing Then
                    mContent = New WriteToCRMProperties
                End If
            End If
            Return mContent
        End Get
    End Property

    Protected Sub btnClickMe_Click(sender As Object, e As EventArgs) Handles btnClickMe.Click
        Try
            'needs a project reference to "Blackbaud.AppFx.WebAPI.dll" and "Blackbaud.AppFx.XmlTypes.dll"

            'create a data form save request
            Dim req As New Blackbaud.AppFx.WebAPI.ServiceProxy.DataFormSaveRequest

            'authenticate this form save request against CRM with BBIS credentials
            req.ClientAppInfo = BBNCExtensions.API.NetCommunity.Current.AppFxWebServiceProvider.CreateClientAppInfoRequestHeader()

            'this is the DataFormInstanceID of the add form you created and loaded into CRM
            req.FormID = New Guid("858e867c-77a1-4816-9588-74e09900b25c")

            'fill in a "dataformitem" with all the field values you need.  In this case, there is only one.
            req.DataFormItem = New Blackbaud.AppFx.XmlTypes.DataForms.DataFormItem With {
                .Values = New Blackbaud.AppFx.XmlTypes.DataForms.DataFormFieldValueSet From {
                New Blackbaud.AppFx.XmlTypes.DataForms.DataFormFieldValue("LOGMESSAGE", txbLogMessage.Text)
            }}

            'grab a connection to CRM.
            Dim appFx = BBNCExtensions.API.NetCommunity.Current.AppFxWebServiceProvider.CreateAppFxWebService()

            'save the record, capture the ID generated by the Add form (this is the @ID output param in SQL).
            'THIS IS WHERE THE ACTUAL RECORD IS WRITTEN
            Dim createdRecordID As String = appFx.DataFormSave(req).ID

            'show feedback to the user
            lblError.Text = "Record was created with ID " + createdRecordID
        Catch ex As Exception
            'Unnecessary.  Errors never happen.
            lblError.Text = ex.Message
        End Try
    End Sub
End Class